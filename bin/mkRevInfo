#!/usr/bin/perl

use strict ;
use warnings ;
use Getopt::Long ;

my $book = 0 ;
my $onlyrepo = 0 ;
my $onlycommit = 0 ;
my $onlycommitdate = 0 ;
my $mytime = 0 ;
my $define = '' ;
my $newline = 1 ;
my $headerName = '' ;
my $lastCommit ;
my $commitDate ;

GetOptions( 
   'book!'      => \$book,
   'mytime!'    => \$mytime,
   'commit!'    => \$onlycommit,
   'date!'      => \$onlycommitdate,
   'repo!'      => \$onlyrepo,
   'newline!'   => \$newline,
   'define=s'   => \$define,
   'header=s'   => \$headerName,
   'help'       => sub { die "usage: mkRevInfo [-book] [-commit] [-date] [-repo]\n" ; },
) ;

# count version from when I made my macbook clone:
my $r = `git rev-list --count fb9dccc` ; chomp $r ;

open my $fh, "git log -1 |" or die ;
while (<$fh>)
{
   chomp ;

   if ( s/commit\s*// )
   {
      $lastCommit = $_ ;
   }
   elsif ( s/^Date:\s*// )
   {
      # Date:   Sun Feb 12 13:38:15 2012 -0500

      # strip timezone:
      s/\s*-\d+// ;

      # strip day of week:
      s/\S+\s+// ;

      # strip hour:min:sec
      s/\d\d:\d\d:\d\d\s+// ;

      # convert spaces to /'s
      s, ,/,g ;

      #s/\s+$// ;

      $commitDate = $_ ;
      last ;
   }
}
close $fh ;

my $url = "https://github.com/peeterjoot/physicsplay" ;
#$url = "http://goo.gl/j2GYL" ;
my $output = '' ;

if ( $mytime )
{
   my $timeString = $commitDate ;
   die unless ( $timeString =~ m,(.*)/\d+/(.*), ) ;

   my ($mon, $year) = ( $1, $2 ) ;

   my %longMonth = (
      'Jan' => 'January',
      'Feb' => 'February',
      'Mar' => 'March',
      'Apr' => 'April',
      'May' => 'May',
      'Jun' => 'June',
      'Jul' => 'July',
      'Aug' => 'August',
      'Sep' => 'September',
      'Oct' => 'October',
      'Nov' => 'November',
      'Dec' => 'December'
   ) ;

   $output = qq@\\newcommand{\\myTime}{$longMonth{$mon} $year}@ ;
}
elsif ( $book )
{
   $output = qq@
Version 0.$r

Sources for this notes compilation can be found in the github repository

\\href{$url}{$url}

The last commit ($commitDate), associated with this pdf was

$lastCommit@ ;
}
elsif ( $onlyrepo )
{
   $output = "$url" ;
}
elsif ( $onlycommit )
{
   $output = "$lastCommit" ;
}
elsif ( $onlycommitdate )
{
   $output = "$commitDate" ;
}
else
{
   $output = "Revision 0.$r $url commit $lastCommit $commitDate" ;
}

if ( $define ne '' )
{
   if ( $headerName ne '' )
   {
      $headerName =~ s,.*/,, ;
      $headerName =~ s/\./_/g ;

      print 
         "#if !defined ${headerName}_is_included\n" .
         "#define ${headerName}_is_included\n" .
         "\n" ;
   }

   print qq@#define $define "$output"\n@ ;

   if ( $headerName ne '' )
   {
      print "\n#endif\n" ;
   }
}
else
{
   print $output ;

   print "\n" if ( $newline ) ;
}
